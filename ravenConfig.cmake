cmake_minimum_required(VERSION 3.11)

set(raven_known_comps static shared)
set(raven_comp_static NO)
set(raven_comp_shared NO)
foreach (raven_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (raven_comp IN_LIST raven_known_comps)
        set(raven_comp_${raven_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "raven does not recognize component `${raven_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (raven_comp_static AND raven_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "raven `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(raven_static_targets "${CMAKE_CURRENT_LIST_DIR}/raven-static-targets.cmake")
set(raven_shared_targets "${CMAKE_CURRENT_LIST_DIR}/raven-shared-targets.cmake")

macro(raven_load_targets type)
    if (NOT EXISTS "${raven_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "raven `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${raven_${type}_targets}")
endmacro()

if (raven_comp_static)
    raven_load_targets(static)
elseif (raven_comp_shared)
    raven_load_targets(shared)
elseif (DEFINED raven_SHARED_LIBS AND raven_SHARED_LIBS)
    raven_load_targets(shared)
elseif (DEFINED raven_SHARED_LIBS AND NOT raven_SHARED_LIBS)
    raven_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${raven_shared_targets}")
        raven_load_targets(shared)
    else ()
        raven_load_targets(static)
    endif ()
else ()
    if (EXISTS "${raven_static_targets}")
        raven_load_targets(static)
    else ()
        raven_load_targets(shared)
    endif ()
endif ()
